plugins {
    id 'org.springframework.boot' version '2.5.15'
    id 'io.spring.dependency-management' version '1.1.5'
    id 'java'
    id 'maven-publish'
    id 'signing'
}

group = 'io.github.vaa25'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
    withJavadocJar()
    withSourcesJar()
}

archivesBaseName = 'spring-boot-starter-web-excel'
version = System.getenv('RELEASE_VERSION') ?: "1.0.0"

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'io.github.vaa25:poiji2:1.5.0'
    implementation 'org.apache.poi:poi-ooxml:5.2.5'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

test {
    useJUnit()
}

jar {
    from sourceSets.main.output
    from sourceSets.main.allJava
}

signing {
    sign publishing.publications
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            customizePom(pom)
            groupId group
            artifactId archivesBaseName
            version version

            from components.java
        }
    }
    repositories {
        maven {
            url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            if (project.hasProperty('sonatypeUsername') && project.hasProperty('sonatypePassword')) {
                credentials {
                    username sonatypeUsername
                    password sonatypePassword
                }
            }
        }
    }
}

def customizePom(pom) {
    pom.withXml {
        def root = asNode()

        root.dependencies.removeAll { dep ->
            dep.scope == "test"
        }

        root.children().last() + {
            resolveStrategy = Closure.DELEGATE_FIRST

            description 'Springboot starter for work with excel files'
            name 'spring-boot-starter-web-excel'
            url 'https://github.com/vaa25/spring-boot-starter-web-excel'
            organization {
                name 'com.github.vaa25'
                url 'https://github.com/vaa25'
            }
            issueManagement {
                system 'GitHub'
                url 'https://github.com/vaa25/spring-boot-starter-web-excel/issues'
            }
            licenses {
                license {
                    name 'The Apache License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                }
            }
            scm {
                url 'https://github.com/vaa25/spring-boot-starter-web-excel'
                connection 'scm:https://github.com/vaa25/spring-boot-starter-web-excel.git'
                developerConnection 'scm:git://github.com/vaa25/spring-boot-starter-web-excel.git'
            }
            developers {
                developer {
                    id 'dev'
                    name 'Alexander Vlasov'
                    email 'alex.vlasov.ua@gmail.com'
                }
            }
        }
    }
}

